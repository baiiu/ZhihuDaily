if (isRunAlone.toBoolean()) {
    apply plugin: 'com.android.application'
} else {
    apply plugin: 'com.android.library'
}

apply plugin: 'com.jakewharton.butterknife'
apply plugin: 'me.tatarka.retrolambda'


android {
    def cfg = rootProject.ext.androidVersionConfig

    compileSdkVersion cfg.androidCompileSdkVersion
    buildToolsVersion cfg.androidBuildToolsVersion

    defaultConfig {
        minSdkVersion cfg.androidMinSdkVersion
        targetSdkVersion cfg.androidTargetSdkVersion
        versionCode 1
        versionName "1.0"
    }

    compileOptions {
        sourceCompatibility rootProject.ext.javaSourceCompatibility as JavaVersion
        targetCompatibility rootProject.ext.javaTargetCompatibility as JavaVersion
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }

        debug {}
    }

    sourceSets {
        main {

            if (isRunAlone.toBoolean()) {
                manifest.srcFile 'src/main/runalone/AndroidManifest.xml'
                java.srcDirs = ['src/main/java', 'src/main/runalone/java', 'src/main/java-gen']
                res.srcDirs = ['src/main/res', 'src/main/runalone/res']
            } else {
                java.srcDirs = ['src/main/java', 'src/main/java-gen']
            }
        }
    }

    resourcePrefix "daily_"
}

dependencies {
    compile fileTree(dir: 'libs', include: ['*.jar'])
    compile rootProject.ext.majorFrame.ButterKnife as String
    annotationProcessor rootProject.ext.majorFrame.aptButterKnifeCompiler as String

    //数据库
    compile 'de.greenrobot:greendao:2.1.0'

    compile project(':module_common:lib_common')
    compile project(':module_service')
}
